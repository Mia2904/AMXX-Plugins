/* Plugin generated by AMXX-Studio */

#include <amxmodx>
#include <fvault>

#define PLUGIN "Sorteo VIP"
#define VERSION "0.1"
#define AUTHOR "Mia2904"

new const g_ChatPrefix[] = "[Sorteo VIP]";

new g_cb_item_disabled;

new g_inscrito;
new g_introd;
new g_menu[33];

public plugin_init()
{
	register_plugin(PLUGIN, VERSION, AUTHOR)
	
	register_clcmd("say /participar", "clcmd_participar");
	
	register_clcmd("nombre_real", "clcmd_nombre_real");
	register_clcmd("motivo_vip", "clcmd_motivo");
	register_clcmd("mod_favorito", "clcmd_mod_favorito");
	register_clcmd("experiencia_adm", "clcmd_experiencia_adm");
	
	g_cb_item_disabled = menu_makecallback("cb_disabled");
}

public client_putinserver(id)
{
	g_inscrito &= ~(1<<(id-1));
}

public client_disconnect(id)
{
	if (g_menu[id])
		menu_destroy(g_menu[id]);
	
	g_menu[id] = 0;
}

public cb_disabled(id, menu, item)
{
	return ITEM_DISABLED;
}

public clcmd_participar(id)
{
	new name[32]
	get_user_name(id, name, 31);
	if (g_inscrito & (1<<(id-1)) || fvault_get_keynum("sorteo.txt", name) != -1)
	{
		colored_print(id, "Ya te has inscrito :)");
		g_inscrito |= (1<<(id-1));
		return PLUGIN_HANDLED;
	}
	
	if (g_menu[id])
	{
		menu_destroy(g_menu[id]);
		g_menu[id] = 0;
	}
	
	new menu = menu_create("\r[MultiMods]\y Inscripcion en el sorteo de VIPs!", "handle_menu");
	//new reason[50];
	//new modfav[50];
	
	//menu_item_setname(menu, 
	menu_additem(menu, "Mod favorito\d - Presiona 1 para introducir", "");
	menu_additem(menu, "Por que deseas ser VIP?\d - Presiona 2 para introducir", "");
	menu_additem(menu, "Experiencia como VIP o ADMIN\d - Presiona 3 para introducir", "");
	menu_additem(menu, "Tu nombre real para contactarte\d - Presiona 4 para introducir^n", "");
	menu_additem(menu, "\yInscribirse!^n", .callback = g_cb_item_disabled);
	menu_additem(menu, "Cancelar");
	
	menu_setprop(menu, MPROP_NUMBER_COLOR, "\r");
	menu_setprop(menu, MPROP_EXIT, MEXIT_NEVER);
	
	menu_display(id, menu);
	
	return PLUGIN_CONTINUE;
}

public handle_menu(id, menu, item)
{
	if (!is_user_connected(id) || !(0 <= item <= 5))
	{
		g_menu[id] = 0;
		menu_destroy(menu);
		return PLUGIN_HANDLED;
	}
	
	if (item == 5)
	{
		g_menu[id] = 0;
		colored_print(id, "Recuerda que puedes inscribirte en cualquier momento!");
		menu_destroy(menu);
		return PLUGIN_HANDLED;
	}
	
	switch (item)
	{
		case 0:
		{
			client_cmd(id, "messagemode mod_favorito");
			colored_print(id, "Escribe ahora^x03 tu mod favorito^x01 y presiona ENTRAR!");
			g_introd |= (1<<(id-1));
			g_menu[id] = menu;
		}
		case 1:
		{
			client_cmd(id, "messagemode motivo_vip");
			colored_print(id, "Escribe ahora^x03 el motivo por el que quieres ser VIP^x01 y presiona ENTRAR! (maximo 80 caracteres)");
			g_introd |= (1<<(id-1));
			g_menu[id] = menu;
		}
		case 2:
		{
			client_cmd(id, "messagemode experiencia_adm");
			colored_print(id, "Escribe ahora^x03 tu experiencia como VIP o ADMIN^x01 y presiona ENTRAR! (maximo 80 caracteres)");
			g_introd |= (1<<(id-1));
			g_menu[id] = menu;
		}
		case 3:
		{
			client_cmd(id, "messagemode nombre_real");
			colored_print(id, "Escribe ahora^x03 tu nombre real^x01 y presiona ENTRAR!");
			g_introd |= (1<<(id-1));
			g_menu[id] = menu;
		}
		case 4:
		{
			new junk, expe[80], mod_fav[32], motivo[81], nombre[50];
			menu_item_getinfo(menu, 0, junk, mod_fav, 31, "", 0, junk);
			menu_item_getinfo(menu, 1, junk, motivo, 80, "", 0, junk);
			menu_item_getinfo(menu, 2, junk, expe, 79, "", 0, junk);
			menu_item_getinfo(menu, 3, junk, nombre, 49, "", 0, junk);
			
			new data[200];
			new name[32];
			get_user_name(id, name, 31);
			formatex(data, charsmax(data), "REAL_NAME:^"%s^" MOD_FAV:^"%s^" MOT:^"%s^" EXP:^"%s^"", name, nombre, mod_fav, motivo, expe);
			
			fvault_set_data("sorteo.txt", name, data);
			colored_print(id, "Te has incrito en el sorteo de VIPs! Buena suerte :)");
			menu_destroy(menu);
			g_inscrito |= (1<<(id-1));
		}
	}
	
	return PLUGIN_HANDLED;
}

public clcmd_nombre_real(id)
{
	if (g_introd & (1<<(id-1)) && g_menu[id])
	{
		new name[50];
		read_argv(1, name, 49);
		trim(name);
		remove_quotes(name);
		menu_item_setcmd(g_menu[id], 3, name);
		menu_item_setname(g_menu[id], 3, "Tu nombre real para contactarte\y - Listo!\d - Presiona 4 para cambiar^n");
		colored_print(id, "Se ha establecido tu nombre real:^x03 %s", name);
		menu_item_setcall(g_menu[id], 4, -1);
		menu_display(id, g_menu[id]);
		g_introd &= ~(1<<(id-1));
	}
}

public clcmd_motivo(id)
{
	if (g_introd & (1<<(id-1)) && g_menu[id])
	{
		new name[81];
		read_argv(1, name, 80);
		trim(name);
		remove_quotes(name);
		menu_item_setcmd(g_menu[id], 1, name);
		menu_item_setname(g_menu[id], 1, "Por que deseas ser VIP?\y - Listo!\d - Presiona 2 para cambiar");
		colored_print(id, "Se ha establecido tu motivo de participacion:^x03 %s", name);
		menu_item_setcall(g_menu[id], 4, -1);
		menu_display(id, g_menu[id]);
		g_introd &= ~(1<<(id-1));
	}
}

public clcmd_mod_favorito(id)
{
	if (g_introd & (1<<(id-1)) && g_menu[id])
	{
		new name[32];
		read_argv(1, name, 31);
		trim(name);
		remove_quotes(name);
		menu_item_setcmd(g_menu[id], 0, name);
		menu_item_setname(g_menu[id], 0, "Mod favorito\y - Listo!\d - Presiona 1 para cambiar");
		colored_print(id, "Se ha establecido tu mod favorito:^x03 %s", name);
		menu_item_setcall(g_menu[id], 4, -1);
		menu_display(id, g_menu[id]);
		g_introd &= ~(1<<(id-1));
	}
}

public clcmd_experiencia_adm(id)
{
	if (g_introd & (1<<(id-1)) && g_menu[id])
	{
		new name[81];
		read_argv(1, name, 80);
		trim(name);
		remove_quotes(name);
		menu_item_setcmd(g_menu[id], 2, name);
		menu_item_setname(g_menu[id], 2, "Experiencia como VIP o ADMIN\y - Listo!\d - Presiona 3 para cambiar");
		colored_print(id, "Se ha establecido tu experiencia como VIP o ADMIN:^x03 %s", name);
		menu_item_setcall(g_menu[id], 4, -1);
		menu_display(id, g_menu[id]);
		g_introd &= ~(1<<(id-1));
	}
}

stock colored_print(id, const what[], any:...)
{
	static message[190], len = 0;
	
	if (!len)
	{
		len = formatex(message, charsmax(message), "^x04%s^x01 ", g_ChatPrefix);
	}
	
	vformat(message[len], charsmax(message)-len, what, 3);
	
	static msgSayText = 0;
	
	if (!msgSayText)
	{
		msgSayText = get_user_msgid("SayText");
	}
	
	message_begin(id ? MSG_ONE : MSG_ALL, msgSayText, _, id);
	write_byte(33);
	write_string(message);
	message_end();
}
//fvault_set_data("sorteo.txt", name, 
